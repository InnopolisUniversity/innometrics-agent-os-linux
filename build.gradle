plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id 'org.beryx.jlink' version '2.22.0'
}

group 'org.innopolisU'
sourceCompatibility = 11
mainClassName = 'com.application.AppLauncher'
project.description = "Innometrics linux data collector is a tool for collecting Linux OS system activities with respect to the user activities. The data collector is part of innometrics framework which is to track developers and improve a company's software development process. More info here https://innometrics.ru"
project.ext.buildDate = new Date()
project.version = '1.0.1'
def launcherName = 'DataCollectorLinux'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'
    implementation group: 'org.json', name: 'json', version: '20090211'

    implementation "net.java.dev.jna:jna:5.5.0"
    implementation "net.java.dev.jna:jna-platform:5.5.0"
    implementation 'org.xerial:sqlite-jdbc:3.31.1'
}

javafx{
    modules = ['javafx.controls', 'javafx.swing', 'javafx.fxml', 'javafx.media']
    version = '12'
}

task copyLicense {
    outputs.file new File("$buildDir/LICENSE")
    doLast {
        copy {
            from "LICENSE"
            into "$buildDir"
        }
    }
}

jar {
    manifest {
        attributes('Main-Class': "$mainClassName")
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    targetPlatform('linux', '/usr/lib/jvm/adoptopenjdk-11-hotspot-amd64')
    launcher {
        name = "${launcherName}"
    }
    addExtraDependencies("javafx")
    jpackage {
        //jvmArgs = ['-splash:$APPDIR/splash.png']
        jpackageHome = '/usr/lib/jvm/java-14-openjdk-amd64'
        skipInstaller = false
        installerType = 'deb'
        targetPlatformName = 'linux'
        imageOptions = ['--icon', 'src/main/resources/metrics-collector.png']
        installerOptions = ['--resource-dir', "build/jpackage/DataCollectorLinux/lib",
                            '--description', project.description,
                            '--vendor', 'Innopolis University',
                            '--app-version', project.version,
                            '--linux-shortcut',
                            '--linux-deb-maintainer', 'innometrics@innopolis.ru']
    }
}

tasks.jpackageImage.doLast {
    copy {
        from "src/main/resources"
        into "build/jpackage/${launcherName}/lib/app"
    }
}
